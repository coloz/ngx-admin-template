# 小米小爱接入  
==该服务仅面向认证用户和企业用户开放==  

>1. [Blinker小爱同学接入流程](#Blinker小爱同学接入流程 "Blinker小爱同学接入流程")
1. [Blinker小爱同学接口函数](#Blinker小爱同学接口函数 "Blinker小爱同学接口函数")
    1. [小爱设备配置](#小爱设备配置 "小爱设备配置")
    1. [小爱数据管理](#小爱数据管理 "小爱数据管理")
    1. [小爱设备支持的控制类型](#小爱设备支持的控制类型 "小爱设备支持的控制类型")
    1. [小爱电源类的操作接口](#小爱电源类的操作接口 "小爱电源类的操作接口")
    1. [小爱颜色的设置接口](#小爱颜色的设置接口 "小爱颜色的设置接口")
    1. [小爱模式的设置接口](#小爱模式的设置接口 "小爱模式的设置接口")
    1. [小爱亮度的控制接口](#小爱亮度的控制接口 "小爱亮度的控制接口")
    1. [小爱色温的控制接口](#小爱色温的控制接口 "小爱色温的控制接口")
    1. [小爱设备查询接口](#小爱设备查询接口 "小爱设备查询接口")
    1. [小爱支持的语音指令](#小爱支持的语音指令 "小爱支持的语音指令")
    1. [小爱支持控制的模式表](#小爱支持控制的模式表 "小爱支持控制的模式表")

## Blinker小爱同学接入流程
### 设备上线
- 设置接入的设备类型
- 设置接入设备的auth Key  
- 设置SSID/PSWD 或者选择 ESPTOUCH等配网方式  
- 下载代码等待设备接入上线成功  

==authKey对应的设备若需要更换接入的设备类型，重新设置设备类型并下载代码。==  
==代码下载后打开 米家APP->我的->其他平台设备->点灯科技->授权->同步设备，重新同步设备后即可更新设备类型。==  

### 绑定小爱同学
- 打开米家 App。点击右下角“我的”，点击“添加智能设备其他平台设备”，选择“点灯科技”，点击“授权”
- 使用blinker账户登录，完成绑定授权。
- 绑定成功后，在blinker App中配网成功的设备就会出现在您的智能家居设备列表中，可以使用小爱同学进行语音控制  

示例代码:
```arduino
#define BLINKER_WIFI
#define BLINKER_MIOT_OUTLET

#include <Blinker.h>

char auth[] = "Your Device Secret Key";
char ssid[] = "Your WiFi network SSID or name";
char pswd[] = "Your WiFi network WPA password or WEP key";

bool oState = false;

void miotPowerState(const String & state)
{
    BLINKER_LOG("need set power state: ", state);

    if (state == BLINKER_CMD_ON) {
        digitalWrite(LED_BUILTIN, HIGH);

        BlinkerMIOT.powerState("on");
        BlinkerMIOT.print();

        oState = true;
    }
    else if (state == BLINKER_CMD_OFF) {
        digitalWrite(LED_BUILTIN, LOW);

        BlinkerMIOT.powerState("off");
        BlinkerMIOT.print();

        oState = false;
    }
}

void miotQuery(int32_t queryCode)
{
    BLINKER_LOG("MIOT Query codes: ", queryCode);

    switch (queryCode)
    {
        case BLINKER_CMD_QUERY_ALL_NUMBER :
            BLINKER_LOG("MIOT Query All");
            BlinkerMIOT.powerState(oState ? "on" : "off");
            BlinkerMIOT.print();
            break;
        case BLINKER_CMD_QUERY_POWERSTATE_NUMBER :
            BLINKER_LOG("MIOT Query Power State");
            BlinkerMIOT.powerState(oState ? "on" : "off");
            BlinkerMIOT.print();
            break;
        default :
            BlinkerMIOT.powerState(oState ? "on" : "off");
            BlinkerMIOT.print();
            break;
    }
}

void dataRead(const String & data)
{
    BLINKER_LOG("Blinker readString: ", data);

    Blinker.vibrate();
    
    uint32_t BlinkerTime = millis();
    
    Blinker.print("millis", BlinkerTime);
}

void setup()
{
    Serial.begin(115200);
    BLINKER_DEBUG.stream(Serial);

    pinMode(LED_BUILTIN, OUTPUT);
    digitalWrite(LED_BUILTIN, LOW);

    Blinker.begin(auth, ssid, pswd);
    Blinker.attachData(dataRead);
    
    BlinkerMIOT.attachPowerState(miotPowerState);
    BlinkerMIOT.attachQuery(miotQuery);
}

void loop()
{
    Blinker.run();
}
```


## Blinker小爱同学接口函数
### 小爱设备配置
根据你定义的设备品类选择不同的参数用于配置Blinker  

light:
```arduino
#define BLINKER_WIFI
#define BLINKER_MIOT_LIGHT

#include <Blinker.h>
```

outlet:
```arduino
#define BLINKER_WIFI
#define BLINKER_MIOT_OUTLET

#include <Blinker.h>
```

<!-- switch:
```arduino
#define BLINKER_WIFI
#define BLINKER_DUEROS_SWITCH

#include <Blinker.h>
``` -->

sensor:
```arduino
#define BLINKER_WIFI
#define BLINKER_MIOT_SENSOR

#include <Blinker.h>
```

multi_outlet:
```arduino
#define BLINKER_WIFI
#define BLINKER_MIOT_MULTI_OUTLET

#include <Blinker.h>
```
> 同步设备后可显示一个插座和四个插孔(名为插孔的插座设备)  

设备设置后即可使用 **小爱同学** **BlinkerMIOT**  

### 小爱数据管理
智能音箱接口, 反馈数据到小爱同学  
==任何控制务必即时反馈==  
==超时2s以上再反馈，小爱同学就默认设备不在线，小爱同学会反馈“智能家居控制出了点问题”==

**函数** :
- powerState()  
    *BlinkerMIOT.powerState()*  
    反馈电源状态
- mode()  
    *BlinkerMIOT.mode()*  
    反馈运行模式
- color()  
    *BlinkerMIOT.color()*  
    反馈颜色属性
- colorTemp()  
    *BlinkerMIOT.colorTemp()*  
    反馈色温属性    
- brightness()  
    *BlinkerMIOT.brightness()*  
    反馈亮度属性  
- temp()  
    *BlinkerMIOT.temp()*  
    反馈温度属性  
- humi()  
    *BlinkerMIOT.humi()*  
    反馈湿度属性  
- pm25()  
    *BlinkerMIOT.pm25()*  
    反馈PM2.5属性  
- print()  
    *BlinkerMIOT.print()*  
    将以上属性发送出去, 务必最后调用该函数  


### 小爱设备支持的控制类型
- 设备控制
    - 电源类的操作
        - 设备打开
        - 设备关闭
    - 颜色的设置  
    - 模式的设置
    - 亮度的控制
        - 亮度设置指定值
        - 亮度调成最值(最大、最小)
        - 步长设置亮度(调亮、调暗)
    - 色温的控制
        - 色温设置指定值
        - 步长设置色温(调冷、调暖)
- 设备查询
    - 单个属性的查询

### 小爱电源类的操作接口
当小爱同学向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerMIOT.attachPowerState()**  
用户自定义电源类操作的回调函数:
```arduino
void miotPowerState(const String & state)
{
    BLINKER_LOG("need set power state: ", state);

    if (state == BLINKER_CMD_ON) {
        digitalWrite(LED_BUILTIN, HIGH);

        BlinkerMIOT.powerState("on");
        BlinkerMIOT.print();
    }
    else if (state == BLINKER_CMD_OFF) {
        digitalWrite(LED_BUILTIN, LOW);

        BlinkerMIOT.powerState("off");
        BlinkerMIOT.print();
    }
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerMIOT.attachPowerState(miotPowerState);
```

### 小爱颜色的设置接口
当小爱同学向设备发起控制, 设备端需要有对应控制处理函数  
  
**BlinkerMIOT.attachColor()**  
用户自定义颜色设置的回调函数:
```arduino
void miotColor(int32_t color)
{
    BLINKER_LOG("need set color: ", color);

    colorR = color >> 16 & 0xFF;
    colorG = color >>  8 & 0xFF;
    colorB = color       & 0xFF;

    BLINKER_LOG("colorR: ", colorR, ", colorG: ", colorG, ", colorB: ", colorB);

    pixelShow();

    BlinkerMIOT.color(color);
    BlinkerMIOT.print();
}
``` 

<!-- > 务必在回调函数中反馈该控制状态  
> 详见文档末尾[支持控制的颜色表](#支持控制的颜色表 "支持控制的颜色表")
> 详见文档末尾[支持查询的颜色表](#支持查询的颜色表 "支持查询的颜色表")   -->

注册回调函数:
```arduino
BlinkerMIOT.attachColor(miotColor);
```

### 小爱模式的设置接口
当小爱同学向设备发起控制, 设备端需要有对应控制处理函数  
  
**BlinkerMIOT.attachMode()**  
用户自定义模式设置的回调函数:
```arduino
void miotMode(uint8_t mode)
{
    BLINKER_LOG("need set mode: ", mode);

    if (mode == BLINKER_CMD_MIOT_DAY) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MIOT_NIGHT) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MIOT_COLOR) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MIOT_WARMTH) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MIOT_TV) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MIOT_READING) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MIOT_COMPUTER) {
        // Your mode function
    }

    wsMode = mode;

    BlinkerMIOT.mode(mode);
    BlinkerMIOT.print();
}
```
> 务必在回调函数中反馈该控制状态  
> 详见文档末尾[小爱支持控制的模式表](#小爱支持控制的模式表 "小爱支持控制的模式表")  

注册回调函数:
```arduino
BlinkerMIOT.attachMode(miotMode);
```

### 小爱亮度的控制接口
当小爱同学向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerMIOT.attachBrightness()**  
用户自定义亮度控制的回调函数:
```arduino
void miotBright(const String & bright)
{
    BLINKER_LOG("need set brightness: ", bright);

    colorW = bright.toInt();

    BLINKER_LOG("now set brightness: ", colorW);
    
    BlinkerMIOT.brightness(colorW);
    BlinkerMIOT.print();
}
```
> 务必在回调函数中反馈该控制状态  
> 亮度范围为1-100  

注册回调函数:
```arduino
BlinkerMIOT.attachBrightness(miotBright);
```

### 小爱色温的控制接口
当小爱同学向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerMIOT.attachColorTemperature()**  
用户自定义色温控制的回调函数:
```arduino
void miotColoTemp(int32_t colorTemp)
{
    BLINKER_LOG("need set colorTemperature: ", colorTemp);

    colorT = colorTemp;

    BlinkerMIOT.colorTemp(colorTemp);
    BlinkerMIOT.print();
}
```
> 务必在回调函数中反馈该控制状态  
> 色温范围为1000-10000  

注册回调函数:
```arduino
BlinkerMIOT.attachColorTemperature(miotColoTemp);
```

### 小爱设备查询接口
当小爱同学向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerMIOT.attachQuery()**  
用户自定义设备查询的回调函数:
```arduino
void miotQuery(int32_t queryCode)
{
    BLINKER_LOG("MIOT Query codes: ", queryCode);

    switch (queryCode)
    {
        case BLINKER_CMD_QUERY_PM25_NUMBER :
            BLINKER_LOG("MIOT Query PM25");
            BlinkerMIOT.pm25(20);
            BlinkerMIOT.print();
            break;
        case BLINKER_CMD_QUERY_HUMI_NUMBER :
            BLINKER_LOG("MIOT Query HUMI");
            BlinkerMIOT.humi(20);
            BlinkerMIOT.print();
            break;
        case BLINKER_CMD_QUERY_TEMP_NUMBER :
            BLINKER_LOG("MIOT Query TEMP");
            BlinkerMIOT.temp(20);
            BlinkerMIOT.print();
            break;
        case BLINKER_CMD_QUERY_TIME_NUMBER :
            BLINKER_LOG("MIOT Query Time");
            BlinkerMIOT.time(millis());
            BlinkerMIOT.print();
            break;
        default :
            BlinkerMIOT.temp(20);
            BlinkerMIOT.humi(20);
            BlinkerMIOT.pm25(20);
            BlinkerMIOT.co2(20);
            BlinkerMIOT.print();
            break;
    }
}
```
支持查询的状态码: 
- 查询电源状态  ***BLINKER_CMD_QUERY_POWERSTATE_NUMBER***
- 查询PM2.5属性  ***BLINKER_CMD_QUERY_PM25_NUMBER***
- 查询CO2属性  ***BLINKER_CMD_QUERY_CO2_NUMBER***
- 查询温度属性  ***BLINKER_CMD_QUERY_TEMP_NUMBER***
- 查询湿度属性  ***BLINKER_CMD_QUERY_HUMI_NUMBER***  

> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerMIOT.attachQuery(miotQuery);
```

### 小爱支持的语音指令
* 灯
    - 打开/关闭{门口}的{灯}
    - 把{卧室}的{灯}调为{红色}  
    颜色范围为0-16777215(0xFFFFFF)
    - 把{卧室}的{灯}的{亮度调高一点}  
    亮度范围为1-100
    - 把{门口}的{灯}的{色温}调到{1400}  
    色温范围为1000-10000
    - 把{卧室}的{灯}调为{月光模式} 
    - 查询{卧室}的{灯}的状态
* 插座
    - 打开/关闭{厨房}的{插座}
    - {客厅}的{插座}是否打开
* 传感器
    - {设备名}室内{温度}/{湿度}/{PM2.5}/{空气质量}/{二氧化碳含量}

### 小爱支持控制的模式表
|模式|中文描述|
| :-: | :-: |
|DAY|日光|
|NIGHT|月光|
|COLOR|彩光|
|WARMTH|温馨|
|TV|电视模式|
|READING|阅读模式|
|COMPUTER|电脑模式|
