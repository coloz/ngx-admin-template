# 天猫精灵接入流程  

>1. [Blinker天猫精灵接入流程](#Blinker天猫精灵接入流程 "Blinker天猫精灵接入流程")
1. [Blinker天猫精灵接口函数](#Blinker天猫精灵接口函数 "Blinker天猫精灵接口函数")
    1. [天猫精灵设备配置](#天猫精灵设备配置 "天猫精灵设备配置")
    1. [天猫精灵数据管理](#天猫精灵数据管理 "天猫精灵数据管理")
    1. [天猫精灵设备支持的控制类型](#天猫精灵设备支持的控制类型 "天猫精灵设备支持的控制类型")
    1. [天猫精灵电源类的操作接口](#天猫精灵电源类的操作接口 "天猫精灵电源类的操作接口")
    1. [天猫精灵颜色的设置接口](#天猫精灵颜色的设置接口 "天猫精灵颜色的设置接口")
    1. [天猫精灵模式的设置接口](#天猫精灵模式的设置接口 "天猫精灵天猫精灵模式的设置接口")
    1. [天猫精灵亮度的控制接口](#天猫精灵亮度的控制接口 "天猫精灵亮度的控制接口")
    1. [天猫精灵色温的控制接口](#天猫精灵色温的控制接口 "天猫精灵色温的控制接口")
    1. [天猫精灵设备查询接口](#天猫精灵设备查询接口 "天猫精灵设备查询接口")
    1. [天猫精灵支持的语音指令](#天猫精灵支持的语音指令 "天猫精灵支持的语音指令")
    1. [天猫精灵支持控制的颜色表](#天猫精灵支持控制的颜色表 "天猫精灵支持控制的颜色表")
    1. [天猫精灵支持查询的颜色表](#天猫精灵支持查询的颜色表 "天猫精灵支持查询的颜色表")
    1. [天猫精灵支持控制的模式表](#天猫精灵支持控制的模式表 "天猫精灵支持控制的模式表")

## 教程
[示例开发教程](https://www.arduino.cn/thread-83756-1-1.html)  

## Blinker天猫精灵接入流程
### 设备上线
- 设置接入的设备类型
- 设置接入设备的auth Key  
- 设置SSID/PSWD 或者选择 ESPTOUCH等配网方式  
- 下载代码等待设备接入上线成功  

==authKey对应的设备若需要更换接入的设备类型，重新设置设备类型并下载代码。==  
==代码下载后在天猫精灵App中解除blinker账号绑定，重新绑定后即可更新设备类型。==  

### 绑定天猫精灵
- 打开天猫精灵 App。点击右下角“我的”，点击“添加智能设备”  
- 在页面上方搜索栏，搜索blinker，并点击要添加的设备类型  
- 点击绑定账号，使用blinker账户登录，完成绑定  
- 绑定成功后，在blinker App中配网成功的设备就会出现在您的智能家居设备列表中，现在可以使用天猫精灵进行语音控制了  

示例代码:
```arduino
#define BLINKER_PRINT Serial
#define BLINKER_WIFI
#define BLINKER_ALIGENIE_OUTLET

#include <Blinker.h>

char auth[] = "Your Device Secret Key";
char ssid[] = "Your WiFi network SSID or name";
char pswd[] = "Your WiFi network WPA password or WEP key";

bool oState = false;

void aligeniePowerState(const String & state)
{
    BLINKER_LOG("need set power state: ", state);

    if (state == BLINKER_CMD_ON) {
        digitalWrite(LED_BUILTIN, HIGH);

        BlinkerAliGenie.powerState("on");
        BlinkerAliGenie.print();

        oState = true;
    }
    else if (state == BLINKER_CMD_OFF) {
        digitalWrite(LED_BUILTIN, LOW);

        BlinkerAliGenie.powerState("off");
        BlinkerAliGenie.print();

        oState = false;
    }
}

void aligenieQuery(int32_t queryCode)
{
    BLINKER_LOG("AliGenie Query codes: ", queryCode);

    switch (queryCode)
    {
        case BLINKER_CMD_QUERY_ALL_NUMBER :
            BLINKER_LOG("AliGenie Query All");
            BlinkerAliGenie.powerState(oState ? "on" : "off");
            BlinkerAliGenie.print();
            break;
        case BLINKER_CMD_QUERY_POWERSTATE_NUMBER :
            BLINKER_LOG("AliGenie Query Power State");
            BlinkerAliGenie.powerState(oState ? "on" : "off");
            BlinkerAliGenie.print();
            break;
        default :
            BlinkerAliGenie.powerState(oState ? "on" : "off");
            BlinkerAliGenie.print();
            break;
    }
}

void dataRead(const String & data)
{
    BLINKER_LOG("Blinker readString: ", data);

    Blinker.vibrate();
    
    uint32_t BlinkerTime = millis();
    Blinker.print(BlinkerTime);
    Blinker.print("millis", BlinkerTime);
}

void setup()
{
    Serial.begin(115200);

    #if defined(BLINKER_PRINT)
        BLINKER_DEBUG.stream(BLINKER_PRINT);
    #endif

    pinMode(LED_BUILTIN, OUTPUT);
    digitalWrite(LED_BUILTIN, LOW);

    Blinker.begin(auth, ssid, pswd);
    Blinker.attachData(dataRead);
    
    BlinkerAliGenie.attachPowerState(aligeniePowerState);
    BlinkerAliGenie.attachQuery(aligenieQuery);
}

void loop()
{
    Blinker.run();
}
```


## Blinker天猫精灵接口函数
### 天猫精灵设备配置
根据你定义的设备品类选择不同的参数用于配置Blinker  

light:
```arduino
#define BLINKER_WIFI
#define BLINKER_ALIGENIE_LIGHT

#include <Blinker.h>
```

outlet:
```arduino
#define BLINKER_WIFI
#define BLINKER_ALIGENIE_OUTLET

#include <Blinker.h>
```

<!-- switch:
```arduino
#define BLINKER_WIFI
#define BLINKER_ALIGENIE_SWITCH

#include <Blinker.h>
``` -->

sensor:
```arduino
#define BLINKER_WIFI
#define BLINKER_ALIGENIE_SENSOR

#include <Blinker.h>
```

multi_outlet:
```arduino
#define BLINKER_WIFI
#define BLINKER_ALIGENIE_MULTI_OUTLET

#include <Blinker.h>
```
> 同步设备后可显示一个插座和四个插孔(名为插孔的插座设备)  

设备设置后即可使用 **天猫精灵** **BlinkerAliGenie**  

### 天猫精灵数据管理
智能音箱接口, 反馈数据到天猫精灵  
==任何控制务必即时反馈==  
==超时2s以上再反馈，天猫精灵就默认设备不在线，天猫精灵会反馈“智能家居控制出了点问题”==

**函数** :
- powerState()  
    *BlinkerAliGenie.powerState()*  
    反馈电源状态
- mode()  
    *BlinkerAliGenie.mode()*  
    反馈运行模式
- color()  
    *BlinkerAliGenie.color()*  
    反馈颜色属性
- colorTemp()  
    *BlinkerAliGenie.colorTemp()*  
    反馈色温属性
- brightness()  
    *BlinkerAliGenie.brightness()*  
    反馈亮度属性
- temp()  
    *BlinkerAliGenie.temp()*  
    反馈温度属性
- humi()  
    *BlinkerAliGenie.humi()*  
    反馈湿度属性
- pm25()  
    *BlinkerAliGenie.pm25()*  
    反馈PM2.5属性
- print()  
    *BlinkerAliGenie.print()*  
    将以上属性发送出去, 务必最后调用该函数  


### 天猫精灵设备支持的控制类型
- 设备控制
    - 电源类的操作
        - 设备打开
        - 设备关闭
    - 颜色的设置  
    - 模式的设置
    - 亮度的控制
        - 亮度设置指定值
        - 亮度调成最值(最大、最小)
        - 步长设置亮度(调亮、调暗，默认步长 25)
    - 色温的控制
        - 色温设置指定值
        - 步长设置色温(调冷、调暖，默认步长 15)
- 设备查询
    - 所有属性的查询
    - 单个属性的查询

### 天猫精灵电源类的操作接口
当天猫精灵向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerAliGenie.attachPowerState()**  
用户自定义电源类操作的回调函数:
```arduino
void aligeniePowerState(const String & state)
{
    BLINKER_LOG("need set power state: ", state);

    if (state == BLINKER_CMD_ON) {
        digitalWrite(LED_BUILTIN, HIGH);

        BlinkerAliGenie.powerState("on");
        BlinkerAliGenie.print();
    }
    else if (state == BLINKER_CMD_OFF) {
        digitalWrite(LED_BUILTIN, LOW);

        BlinkerAliGenie.powerState("off");
        BlinkerAliGenie.print();
    }
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerAliGenie.attachPowerState(aligeniePowerState);
```

### 天猫精灵颜色的设置接口
当天猫精灵向设备发起控制, 设备端需要有对应控制处理函数  
  
**BlinkerAliGenie.attachColor()**  
用户自定义颜色设置的回调函数:
```arduino
void aligenieColor(const String & color)
{
    BLINKER_LOG("need set color: ", color);

    if (color == "Red") {
        colorR = 255; colorG = 0; colorB = 0;
    }
    else if (color == "Yellow") {
        colorR = 255; colorG = 255; colorB = 0;
    }
    else if (color == "Blue") {
        colorR = 0; colorG = 0; colorB = 255;
    }
    else if (color == "Green") {
        colorR = 0; colorG = 255; colorB = 0;
    }
    else if (color == "White") {
        colorR = 255; colorG = 255; colorB = 255;
    }
    else if (color == "Black") {
        colorR = 0; colorG = 0; colorB = 0;
    }
    else if (color == "Cyan") {
        colorR = 0; colorG = 255; colorB = 255;
    }
    else if (color == "Purple") {
        colorR = 128; colorG = 0; colorB = 128;
    }
    else if (color == "Orange") {
        colorR = 255; colorG = 165; colorB = 0;
    }

    BlinkerAliGenie.color(color);
    BlinkerAliGenie.print();
}
```
> 务必在回调函数中反馈该控制状态  
> 详见文档末尾[天猫精灵支持控制的颜色表](#天猫精灵支持控制的颜色表 "天猫精灵支持控制的颜色表")
> 详见文档末尾[天猫精灵支持查询的颜色表](#天猫精灵支持查询的颜色表 "天猫精灵支持查询的颜色表")  

注册回调函数:
```arduino
BlinkerAliGenie.attachColor(aligenieColor);
```

### 天猫精灵模式的设置接口
当天猫精灵向设备发起控制, 设备端需要有对应控制处理函数  
  
**BlinkerAliGenie.attachMode()**  
用户自定义模式设置的回调函数:
```arduino
void aligenieMode(const String & mode)
{
    BLINKER_LOG("need set mode: ", mode);

    if (mode == BLINKER_CMD_READING) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MOVIE) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_SLEEP) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_HOLIDAY) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MUSIC) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_COMMON) {
        // Your mode function
    }

    wsMode = mode;

    BlinkerAliGenie.mode(mode);
    BlinkerAliGenie.print();
}
```
> 务必在回调函数中反馈该控制状态  
> 详见文档末尾[天猫精灵支持控制的模式表](#天猫精灵支持控制的模式表 "天猫精灵支持控制的模式表")  

注册回调函数:
```arduino
BlinkerAliGenie.attachMode(aligenieMode);
```

**BlinkerAliGenie.attachCancelMode()**  
用户自定义取消模式设置的回调函数:
```arduino
void aligeniecMode(const String & cmode)
{
    BLINKER_LOG("need cancel mode: ", cmode);

    if (cmode == BLINKER_CMD_READING) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_MOVIE) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_SLEEP) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_HOLIDAY) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_MUSIC) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_COMMON) {
        // Your mode function
    }

    wsMode = BLINKER_CMD_COMMON; // new mode

    BlinkerAliGenie.mode(wsMode); // must response
    BlinkerAliGenie.print();
}
```
> 务必在回调函数中反馈该控制状态  
> 详见文档末尾[天猫精灵支持控制的模式表](#天猫精灵支持控制的模式表 "天猫精灵支持控制的模式表")  

注册回调函数:
```arduino
BlinkerAliGenie.attachCancelMode(aligeniecMode);
```

### 天猫精灵亮度的控制接口
当天猫精灵向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerAliGenie.attachBrightness()**  
用户自定义亮度控制的回调函数:
```arduino
void aligenieBright(const String & bright)
{
    BLINKER_LOG("need set brightness: ", bright);

    if (bright == BLINKER_CMD_MAX) {
        colorW = 255;
    }
    else if (bright == BLINKER_CMD_MIN) {
        colorW = 0;
    }
    else {
        colorW = bright.toInt();
    }

    BLINKER_LOG("now set brightness: ", colorW);
    
    BlinkerAliGenie.brightness(colorW);
    BlinkerAliGenie.print();
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerAliGenie.attachBrightness(aligenieBright);
```

**BlinkerAliGenie.attachRelativeBrightness()**  
用户步长设置亮度的回调函数:
```arduino
void aligenieRelativeBright(int32_t bright)
{
    BLINKER_LOG("need set relative brightness: ", bright);

    colorW += bright;

    BlinkerAliGenie.brightness(bright);
    BlinkerAliGenie.print();
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerAliGenie.attachRelativeBrightness(aligenieRelativeBright);
```

### 天猫精灵色温的控制接口
当天猫精灵向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerAliGenie.attachColorTemperature()**  
用户自定义色温控制的回调函数:
```arduino
void aligenieColoTemp(int32_t colorTemp)
{
    BLINKER_LOG("need set colorTemperature: ", colorTemp);

    colorT = colorTemp;

    BlinkerAliGenie.colorTemp(colorTemp);
    BlinkerAliGenie.print();
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerAliGenie.attachColorTemperature(aligenieColoTemp);
```

**BlinkerAliGenie.attachRelativeColorTemperature()**  
用户步长设置亮度的回调函数:
```arduino
void aligenieRelativeColoTemp(int32_t colorTemp)
{
    BLINKER_LOG("need set relative colorTemperature: ", colorTemp);

    colorT += colorTemp;

    BlinkerAliGenie.colorTemp(colorTemp);
    BlinkerAliGenie.print();
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerAliGenie.attachRelativeColorTemperature(aligenieRelativeColoTemp);
```

### 天猫精灵设备查询接口
当天猫精灵向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerAliGenie.attachQuery()**  
用户自定义设备查询的回调函数:
```arduino
void aligenieQuery(int32_t queryCode)
{
    BLINKER_LOG("AliGenie Query codes: ", queryCode);

    switch (queryCode)
    {
        case BLINKER_CMD_QUERY_ALL_NUMBER :
            BLINKER_LOG("AliGenie Query All");
            BlinkerAliGenie.powerState(wsState ? "on" : "off");
            BlinkerAliGenie.color(getColor());
            BlinkerAliGenie.colorTemp(colorT);
            BlinkerAliGenie.brightness(colorW);
            BlinkerAliGenie.print();
            break;
        case BLINKER_CMD_POWERSTATE_NUMBER :
            BLINKER_LOG("AliGenie Query Power State");
            BlinkerAliGenie.powerState(wsState ? "on" : "off");
            BlinkerAliGenie.print();
            break;
        case BLINKER_CMD_COLOR_NUMBER :
            BLINKER_LOG("AliGenie Query Color");
            BlinkerAliGenie.color(getColor());
            BlinkerAliGenie.print();
            break;
        case BLINKER_CMD_QUERY_MODE_NUMBER :
            BLINKER_LOG("AliGenie Query Mode");
            BlinkerAliGenie.mode(wsMode);
            BlinkerAliGenie.print();
            break;
        case BLINKER_CMD_COLORTEMP_NUMBER :
            BLINKER_LOG("AliGenie Query ColorTemperature");
            BlinkerAliGenie.colorTemp(colorT);
            BlinkerAliGenie.print();
            break;
        case BLINKER_CMD_BRIGHTNESS_NUMBER :
            BLINKER_LOG("AliGenie Query Brightness");
            BlinkerAliGenie.brightness(colorW);
            BlinkerAliGenie.print();
            break;
        default :
            break;
    }
}
```
支持查询的状态码: 
- 所有属性查询  ***BLINKER_CMD_QUERY_ALL_NUMBER***
- 查询电源状态  ***BLINKER_CMD_QUERY_POWERSTATE_NUMBER***
- 查询颜色属性  ***BLINKER_CMD_QUERY_COLOR_NUMBER***
- 查询模式属性  ***BLINKER_CMD_QUERY_MODE_NUMBER***
- 查询色温属性  ***BLINKER_CMD_QUERY_COLORTEMP_NUMBER***
- 查询亮度属性  ***BLINKER_CMD_QUERY_BRIGHTNESS_NUMBER***
- 查询温度属性  ***BLINKER_CMD_QUERY_TEMP_NUMBER***
- 查询湿度属性  ***BLINKER_CMD_QUERY_HUMI_NUMBER***
- 查询PM2.5属性 ***BLINKER_CMD_QUERY_PM25_NUMBER***  

> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerAliGenie.attachQuery(aligenieQuery);
```

### 天猫精灵支持的语音指令
* 灯
    - 打开/关闭{门口}的{灯}
    - 把{卧室}的{灯}调为{红色}
    - 把{卧室}的{灯}的{亮度调高一点}
    - 把{卧室}的{灯}调为{影院模式}
    - 把{门口}的{灯}的{色温}调到{1400}
    - 把{卧室}的{灯}的{色温调暖一点}
    - 查询{卧室}的{灯}的状态
* 插座
    - 打开/关闭{厨房}的{插座}
    - {客厅}的{插座}是否打开
* 传感器
    - 查询{客厅}的{传感器}的{温度}/{湿度}/{PM2.5}  

### 天猫精灵支持控制的颜色表
|颜色|中文描述|
| - | - |
|Red	|红色|
|Yellow	|黄色|
|Blue	|蓝色|
|Green	|绿色|
|White	|白色|
|Black	|黑色|
|Cyan	|青色|
|Purple	|紫色|
|Orange	|橙色|  

### 天猫精灵支持查询的颜色表
(支持控制的颜色也可以查询)  

|颜色|中文描述|
| - | - |
|Pink	|粉红|
|Violet	|紫罗兰|
|Magenta	|洋红|
|Indigo	|靛青|
|SlateBlue	|蓝灰色|
|DarkBlue	|深蓝色|
|SkyBlue	|天蓝色|
|DarkCyan	|深青色|
|Beige	|浅褐色|
|Brown	|棕色|

### 天猫精灵支持控制的模式表
|模式|中文描述|
| :-: | :-: |
|reading	|阅读|
|movie	|影院|
|sleep	|睡眠|
|holiday	|假日|
|music	|音乐|
|common	|普通|  
