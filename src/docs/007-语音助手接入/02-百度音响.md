# 百度音响接入流程  

>1. [Blinker小度智能接入流程](#Blinker小度智能接入流程 "Blinker小度智能接入流程")
1. [Blinker小度智能接口函数](#Blinker小度智能接口函数 "Blinker小度智能接口函数")
    1. [小度设备配置](#小度设备配置 "小度设备配置")
    1. [小度数据管理](#小度数据管理 "小度数据管理")
    1. [小度设备支持的控制类型](#小度设备支持的控制类型 "小度设备支持的控制类型")
    1. [小度电源类的操作接口](#小度电源类的操作接口 "小度电源类的操作接口")
    1. [小度颜色的设置接口](#小度颜色的设置接口 "小度颜色的设置接口")
    1. [小度模式的设置接口](#小度模式的设置接口 "小度模式的设置接口")
    1. [小度亮度的控制接口](#小度亮度的控制接口 "小度亮度的控制接口")
    1. [小度设备查询接口](#小度设备查询接口 "小度设备查询接口")
    1. [小度支持的语音指令](#小度支持的语音指令 "小度支持的语音指令")
    1. [小度支持控制的模式表](#小度支持控制的模式表 "小度支持控制的模式表")

## Blinker小度智能接入流程
### 设备上线
- 设置接入的设备类型
- 设置接入设备的auth Key  
- 设置SSID/PSWD 或者选择 ESPTOUCH等配网方式  
- 下载代码等待设备接入上线成功  

==authKey对应的设备若需要更换接入的设备类型，重新设置设备类型并下载代码。==  
==代码下载后对小度说 " 小度小度, 发现设备 "，重新发现设备后即可更新设备类型。==  

### 绑定小度智能
- 打开小度智能 App。点击右下角“我的”，点击“添加智能设备”，选择“blinker”，点击“绑定账号”
- 使用blinker账户登录，完成绑定。
- 绑定成功后，在blinker App中配网成功的设备就会出现在您的智能家居设备列表中，可以使用小度智能进行语音控制  

示例代码:
```arduino
#define BLINKER_PRINT Serial
#define BLINKER_WIFI
#define BLINKER_DUEROS_OUTLET

#include <Blinker.h>

char auth[] = "Your Device Secret Key";
char ssid[] = "Your WiFi network SSID or name";
char pswd[] = "Your WiFi network WPA password or WEP key";

bool oState = false;

void duerPowerState(const String & state)
{
    BLINKER_LOG("need set power state: ", state);

    if (state == BLINKER_CMD_ON) {
        digitalWrite(LED_BUILTIN, HIGH);

        BlinkerDuerOS.powerState("on");
        BlinkerDuerOS.print();

        oState = true;
    }
    else if (state == BLINKER_CMD_OFF) {
        digitalWrite(LED_BUILTIN, LOW);

        BlinkerDuerOS.powerState("off");
        BlinkerDuerOS.print();

        oState = false;
    }
}

void duerQuery(int32_t queryCode)
{
    BLINKER_LOG("DuerOS Query codes: ", queryCode);

    switch (queryCode)
    {
        case BLINKER_CMD_QUERY_TIME_NUMBER :
            BLINKER_LOG("DuerOS Query time");
            BlinkerDuerOS.time(millis());
            BlinkerDuerOS.print();
            break;
        default :
            BlinkerDuerOS.time(millis());
            BlinkerDuerOS.print();
            break;
    }
}

void dataRead(const String & data)
{
    BLINKER_LOG("Blinker readString: ", data);

    Blinker.vibrate();
    
    uint32_t BlinkerTime = millis();
    Blinker.print(BlinkerTime);
    Blinker.print("millis", BlinkerTime);
}

void setup()
{
    Serial.begin(115200);

    #if defined(BLINKER_PRINT)
        BLINKER_DEBUG.stream(BLINKER_PRINT);
    #endif

    pinMode(LED_BUILTIN, OUTPUT);
    digitalWrite(LED_BUILTIN, LOW);

    Blinker.begin(auth, ssid, pswd);
    Blinker.attachData(dataRead);
    
    BlinkerDuerOS.attachPowerState(duerPowerState);
    BlinkerDuerOS.attachQuery(duerQuery);
}

void loop()
{
    Blinker.run();
}
```


## Blinker小度智能接口函数
### 小度设备配置
根据你定义的设备品类选择不同的参数用于配置Blinker  

light:
```arduino
#define BLINKER_WIFI
#define BLINKER_DUEROS_LIGHT

#include <Blinker.h>
```

outlet:
```arduino
#define BLINKER_WIFI
#define BLINKER_DUEROS_OUTLET

#include <Blinker.h>
```

<!-- switch:
```arduino
#define BLINKER_WIFI
#define BLINKER_DUEROS_SWITCH

#include <Blinker.h>
``` -->

sensor:
```arduino
#define BLINKER_WIFI
#define BLINKER_DUEROS_SENSOR

#include <Blinker.h>
```

multi_outlet:
```arduino
#define BLINKER_WIFI
#define BLINKER_DUEROS_MULTI_OUTLET

#include <Blinker.h>
```
> 同步设备后可显示一个插座和四个插孔(名为插孔的插座设备)  

设备设置后即可使用 **小度智能** **BlinkerDuerOS**  

### 小度数据管理
智能音箱接口, 反馈数据到小度智能  
==任何控制务必即时反馈==  
==超时2s以上再反馈，小度智能就默认设备不在线，小度智能会反馈“智能家居控制出了点问题”==

**函数** :
- powerState()  
    *BlinkerDuerOS.powerState()*  
    反馈电源状态
- mode()  
    *BlinkerDuerOS.mode()*  
    反馈运行模式
- color()  
    *BlinkerDuerOS.color()*  
    反馈颜色属性
- brightness()  
    *BlinkerDuerOS.brightness()*  
    反馈亮度属性
- temp()  
    *BlinkerDuerOS.temp()*  
    反馈温度属性
- humi()  
    *BlinkerDuerOS.humi()*  
    反馈湿度属性
- pm25()  
    *BlinkerDuerOS.pm25()*  
    反馈PM2.5属性
- pm10()  
    *BlinkerDuerOS.pm10()*  
    反馈PM10属性
- co2()  
    *BlinkerDuerOS.co2()*  
    反馈CO2属性
- aqi()  
    *BlinkerDuerOS.aqi()*  
    反馈AQI属性
- time()  
    *BlinkerDuerOS.time()*  
    反馈设备运行时间
- print()  
    *BlinkerDuerOS.print()*  
    将以上属性发送出去, 务必最后调用该函数  


### 小度设备支持的控制类型
- 设备控制
    - 电源类的操作
        - 设备打开
        - 设备关闭
    - 颜色的设置  
    - 模式的设置
    - 亮度的控制
        - 亮度设置指定值
        - 亮度调成最值(最大、最小)
        - 步长设置亮度(调亮、调暗，默认步长 25)
- 设备查询
    - 单个属性的查询

### 小度电源类的操作接口
当小度智能向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerDuerOS.attachPowerState()**  
用户自定义电源类操作的回调函数:
```arduino
void duerPowerState(const String & state)
{
    BLINKER_LOG("need set power state: ", state);

    if (state == BLINKER_CMD_ON) {
        digitalWrite(LED_BUILTIN, HIGH);

        BlinkerDuerOS.powerState("on");
        BlinkerDuerOS.print();
    }
    else if (state == BLINKER_CMD_OFF) {
        digitalWrite(LED_BUILTIN, LOW);

        BlinkerDuerOS.powerState("off");
        BlinkerDuerOS.print();
    }
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerDuerOS.attachPowerState(duerPowerState);
```

### 小度颜色的设置接口
当小度智能向设备发起控制, 设备端需要有对应控制处理函数  
  
**BlinkerDuerOS.attachColor()**  
用户自定义颜色设置的回调函数:
```arduino
void duerColor(int32_t color)
{
    BLINKER_LOG("need set color: ", color);

    colorR = color >> 16 & 0xFF;
    colorG = color >>  8 & 0xFF;
    colorB = color       & 0xFF;

    BLINKER_LOG("colorR: ", colorR, ", colorG: ", colorG, ", colorB: ", colorB);

    pixelShow();

    BlinkerDuerOS.color(color);
    BlinkerDuerOS.print();
}
``` 

<!-- > 务必在回调函数中反馈该控制状态  
> 详见文档末尾[支持控制的颜色表](#支持控制的颜色表 "支持控制的颜色表")
> 详见文档末尾[支持查询的颜色表](#支持查询的颜色表 "支持查询的颜色表")   -->

注册回调函数:
```arduino
BlinkerDuerOS.attachColor(duerColor);
```

### 小度模式的设置接口
当小度智能向设备发起控制, 设备端需要有对应控制处理函数  
  
**BlinkerDuerOS.attachMode()**  
用户自定义模式设置的回调函数:
```arduino
void duerMode(const String & mode)
{
    BLINKER_LOG("need set mode: ", mode);

    if (mode == BLINKER_CMD_READING) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MOVIE) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_SLEEP) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_HOLIDAY) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_MUSIC) {
        // Your mode function
    }
    else if (mode == BLINKER_CMD_COMMON) {
        // Your mode function
    }

    wsMode = mode;

    BlinkerDuerOS.mode(mode);
    BlinkerDuerOS.print();
}
```
> 务必在回调函数中反馈该控制状态  
> 详见文档末尾[小度支持控制的模式表](#小度支持控制的模式表 "小度支持控制的模式表")  

注册回调函数:
```arduino
BlinkerDuerOS.attachMode(duerMode);
```

**BlinkerDuerOS.attachCancelMode()**  
用户自定义取消模式设置的回调函数:
```arduino
void duercMode(const String & cmode)
{
    BLINKER_LOG("need cancel mode: ", cmode);

    if (cmode == BLINKER_CMD_READING) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_MOVIE) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_SLEEP) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_HOLIDAY) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_MUSIC) {
        // Your mode function
    }
    else if (cmode == BLINKER_CMD_COMMON) {
        // Your mode function
    }

    wsMode = BLINKER_CMD_COMMON; // new mode

    BlinkerDuerOS.mode(wsMode); // must response
    BlinkerDuerOS.print();
}
```
> 务必在回调函数中反馈该控制状态  
> 详见文档末尾[小度支持控制的模式表](#小度支持控制的模式表 "小度支持控制的模式表")  

注册回调函数:
```arduino
BlinkerDuerOS.attachCancelMode(duercMode);
```

### 小度亮度的控制接口
当小度智能向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerDuerOS.attachBrightness()**  
用户自定义亮度控制的回调函数:
```arduino
void duerBright(const String & bright)
{
    BLINKER_LOG("need set brightness: ", bright);

    if (bright == BLINKER_CMD_MAX) {
        colorW = 255;
    }
    else if (bright == BLINKER_CMD_MIN) {
        colorW = 0;
    }
    else {
        colorW = bright.toInt();
    }

    BLINKER_LOG("now set brightness: ", colorW);
    
    BlinkerDuerOS.brightness(colorW);
    BlinkerDuerOS.print();
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerDuerOS.attachBrightness(duerBright);
```

**BlinkerDuerOS.attachRelativeBrightness()**  
用户步长设置亮度的回调函数:
```arduino
void duerRelativeBright(int32_t bright)
{
    BLINKER_LOG("need set relative brightness: ", bright);

    colorW += bright;

    BlinkerDuerOS.brightness(bright);
    BlinkerDuerOS.print();
}
```
> 务必在回调函数中反馈该控制状态  

注册回调函数:
```arduino
BlinkerDuerOS.attachRelativeBrightness(duerRelativeBright);
```

### 小度设备查询接口
当小度智能向设备发起控制, 设备端需要有对应控制处理函数  

**BlinkerDuerOS.attachQuery()**  
用户自定义设备查询的回调函数:
```arduino
void duerQuery(int32_t queryCode)
{
    BLINKER_LOG("DuerOS Query codes: ", queryCode);

    switch (queryCode)
    {
        case BLINKER_CMD_QUERY_AQI_NUMBER :
            BLINKER_LOG("DuerOS Query AQI");
            BlinkerDuerOS.aqi(20);
            BlinkerDuerOS.print();
            break;
        case BLINKER_CMD_QUERY_CO2_NUMBER :
            BLINKER_LOG("DuerOS Query CO2");
            BlinkerDuerOS.co2(20);
            BlinkerDuerOS.print();
            break;
        case BLINKER_CMD_QUERY_PM10_NUMBER :
            BLINKER_LOG("DuerOS Query PM10");
            BlinkerDuerOS.pm10(20);
            BlinkerDuerOS.print();
            break;
        case BLINKER_CMD_QUERY_PM25_NUMBER :
            BLINKER_LOG("DuerOS Query PM25");
            BlinkerDuerOS.pm25(20);
            BlinkerDuerOS.print();
            break;
        case BLINKER_CMD_QUERY_HUMI_NUMBER :
            BLINKER_LOG("DuerOS Query HUMI");
            BlinkerDuerOS.humi(20);
            BlinkerDuerOS.print();
            break;
        case BLINKER_CMD_QUERY_TEMP_NUMBER :
            BLINKER_LOG("DuerOS Query TEMP");
            BlinkerDuerOS.temp(20);
            BlinkerDuerOS.print();
            break;
        case BLINKER_CMD_QUERY_TIME_NUMBER :
            BLINKER_LOG("DuerOS Query Time");
            BlinkerDuerOS.time(millis());
            BlinkerDuerOS.print();
            break;
        default :
            BlinkerDuerOS.temp(20);
            BlinkerDuerOS.humi(20);
            BlinkerDuerOS.pm25(20);
            BlinkerDuerOS.pm10(20);
            BlinkerDuerOS.co2(20);
            BlinkerDuerOS.aqi(20);
            BlinkerDuerOS.time(millis());
            BlinkerDuerOS.print();
            break;
    }
}
```
支持查询的状态码: 
- 查询AQI属性  ***BLINKER_CMD_QUERY_AQI_NUMBER***
- 查询CO2属性  ***BLINKER_CMD_QUERY_CO2_NUMBER***
- 查询PM2.5属性  ***BLINKER_CMD_QUERY_PM25_NUMBER***
- 查询PM10属性  ***BLINKER_CMD_QUERY_PM10_NUMBER***
- 查询温度属性  ***BLINKER_CMD_QUERY_TEMP_NUMBER***
- 查询湿度属性  ***BLINKER_CMD_QUERY_HUMI_NUMBER***
- 查询设备运行时间属性 ***BLINKER_CMD_QUERY_TIME_NUMBER***  

### 小度支持的语音指令
* 灯
    - 打开/关闭{门口}的{灯}
    - 把{卧室}的{灯}调为{红色}
    - 把{卧室}的{灯}的{亮度调高一点}
    - 把{卧室}的{灯}调为{影院模式}
    - 查询{卧室}的{灯}的状态
* 插座
    - 打开/关闭{厨房}的{插座}
    - {客厅}的{插座}是否打开
* 传感器
    - 查询{客厅}的{传感器}的{温度}/{湿度}/{PM2.5}  

### 小度支持控制的模式表
|模式|中文描述|
| :-: | :-: |
|READING|阅读|
|SLEEP|睡眠|
|ALARM|报警|
|NIGHT_LIGHT|夜灯|
|ROMANTIC|浪漫|
|SUNDOWN|日落|
|SUNRISE|日出|
|RELAX|休闲/放松|
|LIGHTING|照明|
|SUN|太阳|
|STAR|星星|
|ENERGY_SAVING|节能|
|MOON|月亮|
|JUDI|蹦迪|
